apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.uban.rent"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [BAIDU_CHANNEL_VALUE: "baidu"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            // No debug config
        }

        release {
            storeFile
            storePassword
            keyAlias
            keyPassword
        }
    }
    getSigningProperties()

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "uban_short_rent_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
// 百度多渠道打包
    productFlavors {
        baidu {}
        /* wandoujia {}
        quihu360 {}
        xiaomi {}
        tencent {}*/
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [BAIDU_CHANNEL_VALUE: name]
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def getSigningProperties() {

    def propFile = file('signing.properties')
    if (propFile.canRead()) {
        def Properties props = new Properties()
        props.load(new FileInputStream(propFile))
        if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println 'signing.properties found but some entries are missing'
            android.buildTypes.release.signingConfig = null
        }
    } else {
        println 'signing.properties not found'
        android.buildTypes.release.signingConfig = null
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile files('libs/baidumapapi_base_v4_1_1.jar')
    compile files('libs/baidumapapi_cloud_v4_1_1.jar')
    compile files('libs/baidumapapi_map_v4_1_1.jar')
    compile files('libs/baidumapapi_radar_v4_1_1.jar')
    compile files('libs/baidumapapi_search_v4_1_1.jar')
    compile files('libs/baidumapapi_util_v4_1_1.jar')
    compile files('libs/locSDK_6.13.jar')
    compile files('libs/Baidu_Mtj_3.7.4.9.jar')
    compile files('libs/PanoramaGL.0.2.jar')
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.trello:rxlifecycle:0.6.1'
    compile 'com.trello:rxlifecycle-components:0.6.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.github.paolorotolo:appintro:4.1.0'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.8.2@aar'
    compile 'com.github.chrisbanes:PhotoView:1.3.1'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.github.paolorotolo:appintro:4.1.0'
    compile files('libs/libammsdk.jar')
}
